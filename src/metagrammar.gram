GRAM      := RULEDEF+;
RULEDEF   := _ name=NAME _ ':=' _ rule=RULE _ ';' _;
RULE      := head=ALT tail={_ '\|' _ alt=ALT }*;
ALT       := MATCHSPEC+;
MATCHSPEC := _ name=NAME '=' rule=POSTOP _
           | _ rule=POSTOP _;
POSTOP    := at=PREOP op='\+|\*'
           | PREOP;
PREOP     := op='\&' at=ATOM
           | ATOM;
ATOM      := name=NAME
           | match=STRLIT
           | '{' _ sub=RULE _ '}';
NAME      := '[a-zA-Z_]+';
STRLIT    := '\'' val='([^\'\\]|(\\.))*' '\'';
_         := '\s*';
